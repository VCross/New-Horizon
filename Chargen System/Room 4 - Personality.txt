Filename: Room 3 - Personality
Date: April 2014
Programmer: Vincent Cross

Purpose:

One of the main rooms of the chargen process. Here the player needs to enter in a detailed personality section that outlines who their character is. There is only the one question.

- Tell us about your character's personality. What makes them who they are, their general mannerisms, their outlook on life in space, how they view other factions/races and all that juicy stuff. Also tell us why you feel that others would want to RP with your character in particular. (room4`personality) (R)
    
- Section status?  C - Complete, I - Incomplete, R - Revisions Required (room4`status)
    
Chargen Code: #1489
Generic Room Parent: #333

-- Room Creation
//
This section contains the code necessary to create the room. It is expected to be performed from Room 3.
!//

@dig Chargen 4 - Personality=Next;n;next,Back;b;back
N
@parent here=Chargen Room Parent
@set here=!no_command
@set here=wizard
@power here=see_all
@desc here=The first step to creating the guts of your new character is their personality. Tell us about your character's personality. What makes them who they are, their general mannerisms, their outlook on life in space, how they view other factions/races and all that juicy stuff. Also tell us why you feel that others would want to RP with your character in particular.%r%rYou can record your personality with '[ansi([u(#1489/desc`color`cmd)],+personality <text>)]'. Remember that you can view your personality in full by simply using '[ansi([u(#1489/desc`color`cmd)],+personality)]'. The 'Current Personality' section below only displays a section of the beginning and end of your personality, as they are often too large to put in this room description.%r%r[u(desc`status)]%r%r[u(desc`personality)]
@lock/command here=checklock/1
&checklock here=[cand([not([u(approved)])],[if([u(#1489/getapp)],1,0)],[switch([u(#1489/getStatus)],S,0,1)])]
&approved here=[hasflag(%#,approved)]
!--

-- Descriptions
//
This section handles the description of the room and what is displayed about the current app's status.
!//

/// Displays the current status of this room.
&desc`status here=[u(#1489/desc`message`currentstatus)][if([hasflag(%#,approved)],[u(#1489/desc`message`alreadyapproved)],[switch([u(#1489/getStatus)],NS,[u(desc`status`editable)],S,[u(#1489/desc`message`appsubmitted)],R,[u(desc`status`editable)])])]

&desc`status`editable here=[switch([u(#1489/getRoom4Status)],I,[u(#1489/desc`message`appincomplete)],C,[ansi([u(#1489/desc`color`complete)],Complete)],R,[u(#1489/desc`message`revisionsrequired)])]

/// Displays the personality section.
&desc`personality here=[ansi([u(#1489/desc`color`question)],Current Personality:)]%r[if([hasflag(%#,approved)],[u(#1489/desc`message`alreadyapproved)],[u(desc`personality`begin)][u(desc`personality`end)])]

/// Displays a section of the personality's beginning.
&desc`personality`begin here=[setq(personality,[u(#1489/getRoom4Personality)])][setq(end,[iter([lpos(%q<personality>,.)],[if([gt([itext(0)],250)],[itext(0)][ibreak()])])])][if(%q<personality>,[left(%q<personality>,[add([if(%q<end>,%q<end>,[strlen(%q<personality>)])],1)])],None)][unsetq(end)]

/// Displays a section of the personality's ending.
&desc`personality`end here=[setq(begin,[iter([lpos(%q<personality>,.)],[if([gt([itext(0)],[sub([setr(length,[strlen(%q<personality>)])],250)])],[itext(0)][ibreak()])])])][if([cand(%q<begin>,[gte([sub(%q<length>,250)],0)])],%r%r...[right(%q<personality>,[sub(%q<length>,%q<begin>,1)])])][unsetq(begin length)]

!--

-- command +personality
//
This command records the player's personality for their character. The no-argument version simply shows what they have given.

In:
	%0 - The given response to the question.
	
Out:
	room4`personality - The DB attribute containing the given response.
!//

&cmd_+personality here=$+personality *:think [u(#1489/room4`personality,%0)];think [u(checkstatus)]

&cmd_+personality-noarg here=$+personality:think [if([u(#1489/hasRoom4Personality)],[pemit(%#,[u(#1489/getRoom4Personality)])],[pemit(%#,Nothing entered yet.)])]

!--

-- function personality
//
The main code to run the +personality command.

In:
	%0 - The given response to the question.
	
Out:
	room4`personality - The DB attribute containing the given response.
!//

&room4`personality #1489=[u(setRoom4Personality,%0)][u(message`4`personality)][u(updateapp,%#)]

&message`4`personality #1489=[pemit(%#,You have set your personality. The abbreviated section in the room's description well tell you if it was cut off for being too large.)]

!--

-- function checkstatus
//
The code in this function involves checking the status of this room to see if it can be considered complete or not. This information is stored in room4`status as C - Complete, I - Incomplete or R - Revisions Required.

Out:
	room4`status - Changes this between C/I/R depending upon the status of the questions.
!//

&checkstatus here=[switch([u(#1489/getRoom4Status)],C,,[pemit(%#,[u(#1489/message`sectioncomplete)])][u(#1489/setRoom4Status,C)])]

!--